;%ifdef CONFIG
;{
;  "RegData": {
;    "RAX": "0x000000000eca8642",
;    "RBX": "0x000000010eca8642",
;    "RCX": "0x000000000eca8642",
;    "RDX": "0x000000010eca8642"
;  },
;  "MemoryRegions": {
;    "0x1000000": "4096"
;  },
;  "MemoryData": {
;    "0x1000000": "0x87654321",
;    "0x1000010": "51 55 55 55 55 55 55 55"
;  }
;}
;%endif

(%ssa1) IRHeader %ssa2, #0
  (%ssa2) CodeBlock %ssa6, %ssa12, %ssa1
    (%ssa6 i0) BeginBlock %ssa2
    %AddrA i64 = Constant #0x1000000
    %MemValueA i32 = LoadMem GPR, #4, %AddrA i64, %Invalid, #4, SXTX, #1
    %Shift i64 = Constant #0x1
    %ResultA i32 = Lshl %MemValueA, %Shift
    %ResultB i64 = Lshl %MemValueA, %Shift
    (%Store i64) StoreContext #8, GPR, %ResultA i64, #0x08
    (%Store i64) StoreContext #8, GPR, %ResultB i64, #0x10
;  Constant optimisable version
    %ValueB i64 = Constant #0x87654321
    %ResultC i32 = Lshl %ValueB, %Shift
    %ResultD i64 = Lshl %ValueB, %Shift
    (%Store i64) StoreContext #8, GPR, %ResultC i64, #0x18
    (%Store i64) StoreContext #8, GPR, %ResultD i64, #0x20
    %BreakRip i64 = EntrypointOffset #0x32, #8
    (%brk i0) Break %BreakRip, {0.11.0.128}
    (%ssa12 i0) EndBlock %ssa2
